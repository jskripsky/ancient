1 CLOSE 1:COLOR 7,0:CLS
2 ON ERROR GOTO 25000
5 KEY OFF
6 KEY 3,"":KEY 4,"":KEY 5,"":KEY 6,"":KEY 7,"":KEY 8,"":KEY 9,"":KEY 10,""
10 REM ************************************************************************
20 REM ***  (C) by KOELLIKER AG SPITALORGANISATION            S C H W E I Z ***
30 REM ************************************************************************
40 REM
50 REM MCS_1000/3 TEST Patientendatenerfassung
60 REM
70 REM ************************************************************************
80 REM ZUERICH 170288
90 REM written by W.H. KOELLIKER AG ZUERICH/SCHWEIZ
100 REM ***********************************************************************
110 REM
120 REM ***********************************************************************
130 REM Edition History
140 REM
150 REM no  date   name         command
160 REM --  ------ ------------ -----------------------------------------------
170 REM 01  160988 W.HOFER      version v 1.00
180 REM                         mcs 1000/3 (koelliker) PaDaE
190 REM                         spur 1  alpha
200 REM                         spur 2  numeric
210 REM                         spur 3  alpha
220 REM   Pinbelegung 2/3 7     datenformat: 2400/8/n/1
230 REM
240 REM ***********************************************************************
510 INBUFF$=""
1000 REM *** START MAINLOOP
1010 REM **********************************************************************
1020 GOSUB 17000
1030 CLS:MASK=1:GOSUB 2000
1310 KEY 1,"ENDE":KEY 2,"PDE"
1320 KEY ON
1500 ON KEY(1) GOSUB 10000
1505 KEY (1) ON
1510 ON KEY(2) GOSUB 11000
1515 KEY (2) ON
1590 IF LOC(1) > 0 THEN GOSUB 16000:LOCATE 5,69:PRINT INBUFF$;
1595 GOTO 1595
1599 REM **********************************************************************
1600 IF LOC(1) > 0 THEN GOSUB 16000:LOCATE 5,69:PRINT INBUFF$;
1610 CLS:MASK=1:GOSUB 2000
1620 KEY (3) OFF
1630 KEY (4) OFF
1640 KEY (5) OFF
1650 KEY (6) OFF
1660 KEY (8) OFF
1670 KEY (9) OFF
1999 GOTO 1310
2000 REM **********************************************************************
2010 REM *** SET SCREEN 1
2020 REM **********************************************************************
2021 IF MASK=1 THEN GOTO 2030
2022 IF MASK=2 THEN GOTO 2300
2023 IF MASK=3 THEN GOTO 2200
2029 RETURN
2030 CLS
2040 COLOR 7,0
2065 LOCATE 2,1
2070 PRINT STRING$(80,205)
2080 COLOR 26:PRINT TAB(2);"                                   MCS - 1000  (P)ATIENTEN (D)ATEN (E)RFASSUNG";
2085 PRINT TAB(2);"OBERDIESSBACH                      (C) 1988 by KOELLIKER AG SPITALORGANISATION"
2090 COLOR 7,0:PRINT STRING$(80,205)
2100 LOCATE 10,21:PRINT"F1 - E N D E"
2110 LOCATE 12,21:PRINT"F2 - Start PDE"
2120 LOCATE 12,36:PRINT "(NUR MAGNETKARTEN-BESCHRIFTUNG UND KORREKTUR)"
2130 LOCATE 21,1:PRINT TAB(21);"Welches Programm wollen Sie: ? ";
2140 LOCATE 20,1:PRINT STRING$(80,205)
2150 LOCATE 22,1:PRINT STRING$(80,205)
2199 RETURN
2200 CLS
2210 COLOR 7,0
2220 LOCATE 2,1
2230 PRINT STRING$(80,205)
2240 PRINT TAB(2);"MAGNETKARTEN-BESCHRIFTUNG          MCS - 1000  (P)ATIENTEN (D)ATEN (E)RFASSUNG";
2250 PRINT TAB(2);"OBERDIESSBACH                      (C) 1988 by KOELLIKER AG SPITALORGANISATION"
2260 PRINT STRING$(80,205)
2270 LOCATE 8,21:PRINT"F3 - KARTE EINLESEN"
2275 LOCATE 16,21:PRINT"F4 - KARTE AUSWERFEN"
2276 LOCATE 14,21:PRINT"F5 - ETIKETTEN DRUCKEN"
2280 LOCATE 10,21:PRINT"F6 - PATIENTENAUFNAHME"
2281 LOCATE 12,21:PRINT"F7 - KARTE KOPIEREN"
2283 LOCATE 18,21:PRINT"F10 - PROGRAMM ENDE "
2285 LOCATE 21,1:PRINT TAB(21);"Welches Programm wollen Sie: ? ";
2286 LOCATE 20,1:PRINT STRING$(80,205)
2287 LOCATE 22,1:PRINT STRING$(80,205)
2299 RETURN
2300 CLS
2365 LOCATE 2,1
2370 PRINT "KOELLIKER AG, ZUERICH       OBERDIESSBACH       (P)ATIENTEN (D)ATEN (E)RFASSUNG";
2390 PRINT STRING$(80,205)
2395 LOCATE 4,1:PRINT" ":COLOR 7,0
2400 LOCATE 4,1:PRINT"P D E  HAUPTPROGRAMM:"
2405 LOCATE 4,55:PRINT"STATUS BOX:              ";
2422 LOCATE 8,1
2427 PRINT STRING$(80,205)
2435 LOCATE 21,1:PRINT "DIALOG BOX:   ---> IHRE AUSWAHL BITTE !                                         ";
2436 LOCATE 23,1:PRINT "STATUS BOX:                                                                     ";
2450 LOCATE 20,1:PRINT STRING$(80,205)
2460 LOCATE 22,1:PRINT STRING$(80,205)
2499 RETURN
2999 RETURN
3000 REM **********************************************************************
3010 REM *** GET KEY INPUT
3020 REM **********************************************************************
3025 COLOR 7,0:LOCATE Y,X:PRINT "_";:LOCATE Y,X
3026 KEYBUFF$="":IN$=""
3030 REM TYPE ABFRAGE EINBAUERN
3040 P=X
3060 IN$=INPUT$(1)
3061 IF ASC(IN$)>95 AND ASC(IN$)<123 THEN IN$=CHR$(ASC(IN$)-32)
3062 IF IN$=CHR$(8) AND LEN(KEYBUFF$) = 0 THEN PRINT CHR$(7);:GOTO 3060
3064 IF IN$=CHR$(8)THEN P=P-1:LOCATE Y,P:PRINT"_";:COLOR 0,7:PRINT " ";:COLOR 7,0:LOCATE Y,P:KEYBUFF$=MID$(KEYBUFF$,1,(LEN(KEYBUFF$)-1)):GOTO 3060
3065 IF IN$=""THEN GOTO 3060
3066 IF IN$=CHR$(13) THEN COLOR 7,0:PRINT " ";:GOTO 3100
3067 IF IN$=CHR$(27) THEN COLOR 7,0:PRINT " ";:GOTO 3100
3070 IF LEN(KEYBUFF$) = L THEN PRINT CHR$(7);:GOTO 3060
3075 P=P+1:PRINT IN$+"_";:LOCATE Y,P
3080 KEYBUFF$=KEYBUFF$+IN$
3090 GOTO 3060
3100 IF LEN(KEYBUFF$) < L AND TYPE$="NUMERIC" THEN KEYBUFF$="0"+KEYBUFF$:GOTO 3100
3105 IF LEN(KEYBUFF$) < L THEN KEYBUFF$=KEYBUFF$+" ":GOTO 3105
3110 COLOR 0,7:LOCATE Y,X:PRINT KEYBUFF$;:COLOR 7,0
3499 RETURN
3500 REM numeric input
3510 IN$ = INKEY$
3520 PRINT IN$
3990 TYPE$=""
3999 RETURN
4000 REM **********************************************************************
4010 REM *** DISPLAY BUFFER DATA
4020 REM **********************************************************************
4170 GOSUB 13000:REM GET STATUS
4200 PATNAME$=MID$(BUFFER1$,1,25)
4210 VORNAME$=MID$(BUFFER1$,26,15)
4250 STRASSE$=MID$(BUFFER1$,41,25)
4260 GESCHL$=MID$(BUFFER1$,66,1)
4270 GARANT$=MID$(BUFFER1$,67,4)
4300 KONF$=MID$(BUFFER1$,71,1)
4310 PATNUM$=MID$(BUFFER2$,1,10)
4320 GEBDAT$=MID$(BUFFER2$,11,8)
4330 EINTRITTDAT$=MID$(BUFFER2$,19,8)
4340 KLASSE$=MID$(BUFFER2$,27,1)
4350 ORT$=MID$(BUFFER3$,1,25)
4360 TEL$=MID$(BUFFER3$,26,15)
4370 ARZT$=MID$(BUFFER3$,41,25)
4500 COLOR 0,7:LOCATE 9,13:PRINT PATNAME$
4510 COLOR 0,7:LOCATE 11,13:PRINT VORNAME$
4550 COLOR 0,7:LOCATE 13,13:PRINT STRASSE$
4560 COLOR 0,7:LOCATE 15,13:PRINT ORT$
4570 COLOR 0,7:LOCATE 17,13:PRINT GEBDAT$
4600 COLOR 0,7:LOCATE 17,22:PRINT GESCHL$
4610 COLOR 0,7:LOCATE 19,13:PRINT TEL$
4620 COLOR 0,7:LOCATE 9,55:PRINT KONF$
4630 COLOR 0,7:LOCATE 11,55:PRINT EINTRITTDAT$
4650 COLOR 0,7:LOCATE 13,55:PRINT PATNUM$
4660 COLOR 0,7:LOCATE 15,55:PRINT KLASSE$
4670 COLOR 0,7:LOCATE 17,55:PRINT GARANT$
4680 COLOR 0,7:LOCATE 19,55:PRINT ARZT$
4900 COLOR 7,0
4999 RETURN
5000 REM **********************************************************************
5010 REM *** WRITE BUFFER TO MCS
5020 REM **********************************************************************
5030 COLOR 0,7
5050 PRINT #1,ESC$;WRITE1$;BUFFER1$;CR$;
5060 LINE INPUT #1,INBUFF$
5070 PRINT #1,ESC$;WRITE2$;BUFFER2$;CR$;
5080 LINE INPUT #1,INBUFF$
5090 PRINT #1,ESC$;WRITE3$;BUFFER3$;CR$;
5100 LINE INPUT #1,INBUFF$
5195 PRINT #1,ESC$;WRITEON$;CR$
5196 LINE INPUT #1,INBUFF$:COLOR 0,7:LOCATE 5,79:PRINT INBUFF$;
5200 COLOR 7,0
5900 LINE INPUT #1,INBUFF$:COLOR 0,7:LOCATE 5,79:PRINT INBUFF$;
5999 RETURN
6000 REM **********************************************************************
6010 REM *** EDIT DATA BUFFER
6020 REM **********************************************************************
6030 MASK=2:GOSUB 2000:GOSUB 14000
6140 COLOR 7,0
6150 LOCATE 23,1:PRINT "STATUS BOX:                                                                     ";
6160 LOCATE 21,1:PRINT "DIALOG BOX:    ----> DATENEINGABE ANGEWAEHLT !!                                 ";
6200 REM ----------------------------------------------------------------------
6210 X=13:Y=9:L=25:TYPE$="":GOSUB 3000:PATNAME$=KEYBUFF$
6211 IF IN$=CHR$(27)THEN GOSUB 30000:PATNAME$=KEYBUFF$
6220 X=13:Y=11:L=15:TYPE$="":GOSUB 3000:VORNAME$=KEYBUFF$
6225 IF IN$=CHR$(27)THEN GOSUB 30000:VORNAME$=KEYBUFF$
6260 X=13:Y=13:L=25:TYPE$="":GOSUB 3000:STRASSE$=KEYBUFF$
6265 IF IN$=CHR$(27)THEN GOSUB 30000:STRASSE$=KEYBUFF$
6270 X=13:Y=15:L=25:TYPE$="":GOSUB 3000:ORT$=KEYBUFF$
6275 IF IN$=CHR$(27)THEN GOSUB 30000:ORT$=KEYBUFF$
6277 X=13:Y=17:L=2:TYPE$="NUMERIC":GOSUB 3000:GEBDAT$=KEYBUFF$+"."
6278 IF IN$=CHR$(27)THEN GOSUB 30000:GEBDAT$=KEYBUFF$+"."
6280 X=16:Y=17:L=2:TYPE$="NUMERIC":GOSUB 3000:GEBDAT$=GEBDAT$+KEYBUFF$+"."
6282 IF IN$=CHR$(27)THEN GOSUB 30000:GEBDAT$=GEBDAT$+KEYBUFF$+"."
6284 X=19:Y=17:L=2:TYPE$="NUMERIC":GOSUB 3000:GEBDAT$=GEBDAT$+KEYBUFF$+"/"
6286 IF IN$=CHR$(27)THEN GOSUB 30000:GEBDAT$=GEBDAT$+KEYBUFF$+"/"
6288 X=22:Y=17:L=1:TYPE$="":GOSUB 3000:GESCHL$=KEYBUFF$
6290 IF IN$=CHR$(27)THEN GOSUB 30000:GESCHL$=KEYBUFF$
6300 X=13:Y=19:L=15:TYPE$="":GOSUB 3000:TEL$=KEYBUFF$
6305 IF IN$=CHR$(27)THEN GOSUB 30000:TEL$=KEYBUFF$
6310 X=55:Y=9:L=1:TYPE$="":GOSUB 3000:KONF$=KEYBUFF$
6311 IF IN$=CHR$(27)THEN GOSUB 30000:KONF$=KEYBUFF$
6312 X=55:Y=11:L=2:TYPE$="NUMERIC":GOSUB 3000:EINTRITTDAT$=KEYBUFF$+"."
6313 IF IN$=CHR$(27)THEN GOSUB 30000:EINTRITTDAT$=KEYBUFF$+"."
6314 X=58:Y=11:L=2:TYPE$="NUMERIC":GOSUB 3000:EINTRITTDAT$=EINTRITTDAT$+KEYBUFF$+"."
6315 IF IN$=CHR$(27)THEN GOSUB 30000:EINTRITTDAT$=EINTRITTDAT$+KEYBUFF$+"."
6320 X=61:Y=11:L=2:TYPE$="NUMERIC":GOSUB 3000:EINTRITTDAT$=EINTRITTDAT$+KEYBUFF$
6321 IF IN$=CHR$(27)THEN GOSUB 30000:EINTRITTDAT$=EINTRITTDAT$+KEYBUFF$
6322 X=55:Y=13:L=5:TYPE$="NUMERIC":GOSUB 3000:PATNUM$=KEYBUFF$+"."
6323 IF IN$=CHR$(27)THEN GOSUB 30000:PATNUM$=KEYBUFF$+"."
6324 X=61:Y=13:L=2:TYPE$="NUMERIC":GOSUB 3000:PATNUM$=PATNUM$+KEYBUFF$+"."
6325 IF IN$=CHR$(27)THEN GOSUB 30000:PATNUM$=PATNUM$+KEYBUFF$+"."
6327 X=64:Y=13:L=1:TYPE$="NUMERIC":GOSUB 3000:PATNUM$=PATNUM$+KEYBUFF$:GOSUB 41000
6330 IF IN$=CHR$(27)THEN GOSUB 30000:PATNUM$=PATNUM$+KEYBUFF$
6350 X=55:Y=15:L=1:TYPE$="NUMERIC":GOSUB 3000:KLASSE$=KEYBUFF$
6355 IF IN$=CHR$(27)THEN GOSUB 30000:KLASSE$=KEYBUFF$
6356 X=55:Y=17:L=4:TYPE$="":GOSUB 3000:GARANT$=KEYBUFF$
6357 IF IN$=CHR$(27)THEN GOSUB 30000:GARANT$=KEYBUFF$
6358 X=55:Y=19:L=25:TYPE$="":GOSUB 3000:ARZT$=KEYBUFF$
6359 IF IN$=CHR$(27)THEN GOSUB 30000:ARZT$=KEYBUFF$
6360 IF IN$=CHR$(13)THEN GOSUB 40000
6500 BUFFER1$=PATNAME$+VORNAME$+STRASSE$+GESCHL$+GARANT$+KONF$
6514 BUFFER2$=MID$(PATNUM$,1,5)+"D"+MID$(PATNUM$,7,2)+"D"+MID$(PATNUM$,10,1)
6515 BUFFER2$=BUFFER2$+MID$(GEBDAT$,1,2)+"D"+MID$(GEBDAT$,4,2)+"D"+MID$(GEBDAT$,7,2)
6516 BUFFER2$=BUFFER2$+MID$(EINTRITTDAT$,1,2)+"D"+MID$(EINTRITTDAT$,4,2)+"D"+MID$(EINTRITTDAT$,7,2)
6517 BUFFER2$=BUFFER2$+KLASSE$
6520 BUFFER3$=ORT$+TEL$+ARZT$
6525 IF LEN(BUFFER2$)<37 THEN BUFFER2$=BUFFER2$+"0":GOTO 6525
6535 LOCATE 21,1:PRINT "DIALOG BOX:    ----> DANKE, EINEN MOMENT ICH UEBERTRAGE DIE DATEN !!            ";
6540 GOSUB 5000
6600 LOCATE 23,1:PRINT "STATUS BOX:                                                                     ";
6610 LOCATE 21,1:PRINT "DIALOG BOX:    ----> BITTE DIE MAGNETKARTE !!                                   ";
6620 GOSUB 16000
6630 GOSUB 13000
6640 LOCATE 21,1:PRINT "DIALOG BOX:    ----> DANKE, DIE KARTE IST BESCHRIEBEN !!                        ";
6650 PRINT #1,ESC$;RETCARD$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
6910 ON KEY (10) GOSUB 6999
6920 KEY (10) ON
6930 ON KEY (9) GOSUB 6998
6940 KEY (9) ON
6950 ON KEY (5) GOSUB 33000
6960 KEY (5) ON
6970 ON KEY (8) GOSUB 34000
6980 KEY (8) ON
6990 GOTO 6910
6998 PRINT #1,ESC$;RETCARD$;CR$:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;:GOSUB 16000:RETURN 6140
6999 PRINT #1,ESC$;RETCARD$;CR$:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;:GOSUB 16000:RETURN 11099
8000 REM **********************************************************************
8010 REM *** PRINT ETIKETTEN
8015 REM **********************************************************************
8021 COLOR 0,7:LOCATE 21,1:PRINT "DIALOG BOX:    ----> AUSDRUCK DER DATEN LŽUFT !!                                ";
8030 PRINT #1,ESC$;PRINTERON$;CR$;:LINE INPUT #1,INBUFF$
8040 PRINT #1,CR$;CHR$(10);
8050 PRINT #1," ";
8060 PRINT #1,PATNAME$;"";PATNUM$;
8065 PRINT #1,CR$;CHR$(10);:GOSUB 26000:GOSUB 26000
8070 PRINT #1," ";
8080 PRINT #1,VORNAME$;"    ";KLASSE$;"    ";GEBDAT$;"/";GESCHL$;
8085 PRINT #1,CR$;CHR$(10);:GOSUB 26000:GOSUB 26000
8090 PRINT #1," ";
8092 PRINT #1,ORT$;"     ";GARANT$;
8094 PRINT #1,CR$;CHR$(10);:GOSUB 26000:GOSUB 26000
8096 PRINT #1," ";
8100 PRINT #1,ARZT$;" ";EINTRITTDAT$;
8105 PRINT #1,CR$;CHR$(10);:GOSUB 26000:GOSUB 26000
8110 PRINT #1," ";
8140 REM PRINT #1,CR$;CHR$(13);
8150 PRINT #1,CR$;CHR$(24);
8900 PRINT #1,ESC$;PRINTEROFF$;CR$;:LINE INPUT #1,INBUFF$
8910 LOCATE 5,79:PRINT INBUFF$;
8920 GOSUB 13000
8990 COLOR 7,0:LOCATE 21,1:PRINT "DIALOG BOX:    ----> AUSDRUCK DER DATEN FERTIG !!                               ";
8999 RETURN
9000 REM **********************************************************************
9010 REM *** COPY SCREEN (BUFFER) TO MCS
9020 REM **********************************************************************
9021 MASK = 2:GOSUB 2000:GOSUB 14000
9022 GOSUB 4000
9025 LOCATE 21,1:PRINT "DIALOG BOX:  ----> COPY PROGRAMM ANGEWAEHLT !  ----> BITTE WARTEN !             ";
9050 GOSUB 5000
9200 COLOR 7,0
9210 LOCATE 21,1:PRINT "DIALOG BOX:  BITTE KARTE EINLEGEN !   OK  J/N                                  ";
9220 IN$= INPUT$(1)
9230 IF IN$="J" OR IN$="j" THEN GOTO 9260
9240 IF IN$="N" OR IN$="n" THEN MASK=1:COLOR 7,0:RETURN 11099
9250 GOTO 9220
9260 PRINT#1,ESC$;RETCARD$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
9900 LOCATE 21,1:PRINT "DIALOG BOX:   ----> OK, KARTE KOPIERT !                                         ";
9905 GOSUB 16000
9910 ON KEY (10) GOSUB 9999
9920 KEY (10) ON
9930 ON KEY (9) GOSUB 9998
9940 KEY (9) ON
9990 GOTO 9990
9998 RETURN 9000
9999 RETURN 11099
10000 REM *********************************************************************
10010 REM *** Return to System
10020 REM *********************************************************************
10030 PRINT #1,ESC$;"H";CR$;:GOSUB 16000:LOCATE 5,79:PRINT INBUFF$;
10035 PRINT #1,ESC$;"R";CR$;:GOSUB 16000:LOCATE 5,79:PRINT INBUFF$;
10040 PRINT #1,ESC$;"F";CR$;:GOSUB 16000:LOCATE 5,79:PRINT INBUFF$;
10045 PRINT #1,ESC$;"R";CR$;:GOSUB 16000:LOCATE 5,79:PRINT INBUFF$;
10990 CLS
10998 GOSUB 18000 : REM CLOSE COM
10999 SYSTEM
11000 REM *** PDE HAUPTPROGRAMM
11010 REM *** PDE HAUPTPROGRAMM
11020 REM *********************************************************************
11025 GOSUB 21000:GOSUB 13000
11030 KEY 3,"READ"
11040 KEY 4,"WRITE"
11050 KEY 5,"PRINT"
11060 KEY 6,"EDIT"
11070 KEY 7,"COPY"
11075 KEY 8,"SDRUCK"
11080 KEY 9,"REPEAT"
11090 KEY 10,"RETURN"
11099 MASK=3:GOSUB 2000:REM INIT SCREEN
11110 ON KEY (3) GOSUB 12000
11115 KEY (3) ON
11120 ON KEY (7) GOSUB 9000
11125 KEY (7) ON
11130 ON KEY (6) GOSUB 6000
11135 KEY (6) ON
11140 ON KEY (4) GOSUB 22000
11145 KEY (4) ON
11155 KEY (5) OFF
11160 KEY (8) OFF
11900 ON KEY(10) GOSUB 11999
11910 KEY (10) ON
11990 GOTO 11990
11999 RETURN 1600
12000 REM *********************************************************************
12010 REM *** READ DATA FROM MCS and DISPLAY
12020 REM *********************************************************************
12025 MASK =2:GOSUB 2000:GOSUB 14000
12030 PRINT #1,ESC$;READON$;CR$:LINE INPUT #1,INBUFF$:LOCATE 4,79:PRINT INBUFF$;
12040 REM
12071 COLOR 0,7:LOCATE 21,1:PRINT "DIALOG BOX:  DIE MAGNETKARTE BITTE !!   OK  J/N !!                              ";
12076 IN$= INPUT$(1)
12077 IF IN$="J" OR IN$="j" THEN GOTO 12080
12078 IF IN$="N" OR IN$="n" THEN MASK=1:COLOR 7,0:RETURN 11099
12079 GOTO 12076
12080 LINE INPUT #1,INBUFF$:LOCATE 4,79:PRINT INBUFF$;:COLOR 0,7
12081 PRINT#1,ESC$;RETCARD$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 4,79:PRINT INBUFF$;
12083 LOCATE 21,1:PRINT "DIALOG BOX:  DANKE, EINEN MOMENT ICH LESE DIE DATEN EIN !!                      ";
12084 LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;:COLOR 0,7
12085 PRINT #1,ESC$;READ1$;CR$;:LINE INPUT#1,INBUFF$:BUFFER1$=INBUFF$
12090 LOCATE 5,1:PRINT"T1: ";:PRINT BUFFER1$;
12100 PRINT #1,ESC$;READ2$;CR$;:LINE INPUT#1,INBUFF$:BUFFER2$=INBUFF$
12110 LOCATE 6,1:PRINT"T2: ";:PRINT BUFFER2$;
12120 PRINT #1,ESC$;READ3$;CR$;:LINE INPUT#1,INBUFF$:BUFFER3$=INBUFF$
12130 LOCATE 7,1:PRINT"T3: ";:PRINT BUFFER3$;
12140 COLOR 7,0
12150 LOCATE 23,1:PRINT "STATUS BOX:                                                                     ";
12160 LOCATE 21,1:PRINT "DIALOG BOX:    ----> IHRE AUSWAHL BITTE !                                       ";
12200 GOSUB 4000
12210 GOSUB 40000
12900 COLOR 7,0
12910 ON KEY (10) GOSUB 12999
12920 KEY (10) ON
12930 ON KEY (9) GOSUB 12998
12940 KEY (9) ON
12950 ON KEY (5) GOSUB 8000
12960 KEY (5) ON
12970 ON KEY (8) GOSUB 34000
12980 KEY (8) ON
12990 GOTO 12910
12998 RETURN 12030
12999 RETURN 11099
13000 REM *********************************************************************
13010 REM *** GET STATUS FROM MCS UNIT and DISPLAY in STATUS BOX
13020 REM *********************************************************************
13030 PRINT #1,ESC$;STATUS$;CR$;
13040 LINE INPUT #1,INBUFF$
13045 IF INBUFF$="?" THEN GOTO 13000
13046 IF INBUFF$="!" THEN GOTO 13000
13050 COLOR 0,7:LOCATE 4,69:PRINT INBUFF$;
13060 STAT1$=MID$(INBUFF$,1,1)
13070 STAT2$=MID$(INBUFF$,2,1)
13080 STAT3$=MID$(INBUFF$,3,1)
13090 STAT4$=MID$(INBUFF$,4,1)
13100 STAT5$=MID$(INBUFF$,5,1)
13110 STAT6$=MID$(INBUFF$,6,1)
13120 STAT7$=MID$(INBUFF$,7,1)
13130 STAT8$=MID$(INBUFF$,8,1)
13200 REM ---------------------------------------------------------------------
13210 REM PRINT EXTENDED STATUS
13220 REM ---------------------------------------------------------------------
13230 IF STAT1$ = "9" THEN LOCATE 23,13:PRINT "T1=OFF";
13240 IF STAT2$ = "9" THEN LOCATE 23,21:PRINT "T2=OFF";
13250 IF STAT3$ = "9" THEN LOCATE 23,29:PRINT "T3=OFF";
13260 IF STAT1$ = "0" THEN LOCATE 23,13:PRINT "T1=ON ";
13270 IF STAT2$ = "0" THEN LOCATE 23,21:PRINT "T2=ON ";
13280 IF STAT3$ = "0" THEN LOCATE 23,29:PRINT "T3=ON ";
13290 IF STAT1$ = "5" THEN LOCATE 23,13:PRINT "T1=ON ";
13300 IF STAT2$ = "5" THEN LOCATE 23,21:PRINT "T2=ON ";
13310 IF STAT3$ = "5" THEN LOCATE 23,29:PRINT "T3=ON ";
13320 IF STAT4$ = "0" THEN LOCATE 23,36:PRINT "READ ";
13330 IF STAT4$ = "1" THEN LOCATE 23,36:PRINT "WRITE";
13340 IF STAT1$ = "1" THEN LOCATE 23,13:PRINT "T1=ON ";:LOCATE 23,50:PRINT "CLOCK error T1";
13350 IF STAT2$ = "1" THEN LOCATE 23,21:PRINT "T2=ON ";:LOCATE 23,50:PRINT "CLOCK error T2";
13360 IF STAT3$ = "1" THEN LOCATE 23,29:PRINT "T3=ON ";:LOCATE 23,50:PRINT "CLOCK error T3";
13370 IF STAT1$ = "2" THEN LOCATE 23,13:PRINT "T1=ON ";:LOCATE 23,50:PRINT "PARITY ERROR T1";
13380 IF STAT2$ = "2" THEN LOCATE 23,21:PRINT "T2=ON ";:LOCATE 23,50:PRINT "PARITY error T2";
13390 IF STAT3$ = "2" THEN LOCATE 23,29:PRINT "T3=ON ";:LOCATE 23,50:PRINT "PARITY error T3";
13400 IF STAT1$ = "3" THEN LOCATE 23,13:PRINT "T1=ON ";:LOCATE 23,50:PRINT "LRC    ERROR T1";
13410 IF STAT2$ = "3" THEN LOCATE 23,21:PRINT "T2=ON ";:LOCATE 23,50:PRINT "LRC    error T2";
13420 IF STAT3$ = "3" THEN LOCATE 23,29:PRINT "T3=ON ";:LOCATE 23,50:PRINT "LRC    error T3";
13430 IF STAT1$ = "4" THEN LOCATE 23,13:PRINT "T1=ON ";:LOCATE 23,50:PRINT "ETX    ERROR T1";
13440 IF STAT2$ = "4" THEN LOCATE 23,21:PRINT "T2=ON ";:LOCATE 23,50:PRINT "ETX    error T2";
13450 IF STAT3$ = "4" THEN LOCATE 23,29:PRINT "T3=ON ";:LOCATE 23,50:PRINT "ETX    error T3";
13900 REM ---------------------------------------------------------------------
13910 REM SET ALL TRACKS  ACTIVE
13920 REM ---------------------------------------------------------------------
13930 IF STAT1$="9" THEN PRINT#1,ESC$;TRACK1$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
13935 IF INBUFF$="?" THEN GOSUB 26000:GOTO 13930
13940 IF STAT2$="9" THEN PRINT#1,ESC$;TRACK2$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
13945 IF INBUFF$="?" THEN GOSUB 26000:GOTO 13940
13950 IF STAT3$="9" THEN PRINT#1,ESC$;TRACK3$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
13955 IF INBUFF$="?" THEN GOSUB 26000:GOTO 13950
13960 IF STAT1$="9" OR STAT2$="9" OR STAT3$="9" THEN GOTO 13000
13998 COLOR 7,0
13999 RETURN
14000 REM *********************************************************************
14010 REM *** SCREENMASK FOR EDIT DATA  NORMAL
14020 REM *********************************************************************
14030 LOCATE 9,1:PRINT"1 NAME      ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14040 LOCATE 11,1:PRINT"2 VORNAME   ";:COLOR 0,7:PRINT"               ";:COLOR 7,0
14070 LOCATE 13,1:PRINT"3 STRASSE   ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14080 LOCATE 15,1:PRINT"4 PLZ, ORT  ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14090 LOCATE 17,1:PRINT"5 GEB-DAT/G ";:COLOR 0,7:PRINT"  .  .  / ";:COLOR 7,0
14100 LOCATE 19,1:PRINT"6 TEL.-NR.  ";:COLOR 0,7:PRINT"               ";:COLOR 7,0
14110 LOCATE 9,39:PRINT"7  KONFESSION   ";:COLOR 0,7:PRINT" ";:COLOR 7,0
14120 LOCATE 11,39:PRINT"8  EINTR.-DAT.  ";:COLOR 0,7:PRINT"  .  .  ";:COLOR 7,0
14150 LOCATE 13,39:PRINT"9  PAT.-NR.     ";:COLOR 0,7:PRINT"     .  . ";:COLOR 7,0
14160 LOCATE 15,39:PRINT"10 KLASSE       ";:COLOR 0,7:PRINT" ";:COLOR 7,0
14170 LOCATE 17,39:PRINT"11 GARANT       ";:COLOR 0,7:PRINT"    ";:COLOR 7,0
14180 LOCATE 19,39:PRINT"12 EINW.-ARZT   ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14200 COLOR 7,0:RETURN
14210 REM *********************************************************************
14220 REM *** SCREENMASK FOR EDIT DATA  INVERS
14230 REM *********************************************************************
14250 LOCATE 10,1:PRINT"1 NAME      ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14260 LOCATE 12,1:PRINT"2 VORNAME   ";:COLOR 0,7:PRINT"              ";:COLOR 7,0
14290 LOCATE 14,1:PRINT"3 STRASSE   ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14300 LOCATE 16,1:PRINT"4 PLZ, ORT  ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14310 LOCATE 18,1:PRINT"5 GEB-DAT/G ";:COLOR 0,7:PRINT"        ";:COLOR 7,0
14320 LOCATE 20,1:PRINT"6 PAT.-NR   ";:COLOR 0,7:PRINT"       ";:COLOR 7,0
14340 LOCATE 10,39:PRINT"7  KLASSE       ";:COLOR 0,7:PRINT"    ";:COLOR 7,0
14350 LOCATE 12,39:PRINT"8  ABTEILUNG    ";:COLOR 0,7:PRINT"    ";:COLOR 7,0
14380 LOCATE 14,39:PRINT"9  EINTR.-DAT   ";:COLOR 0,7:PRINT"  .  .   ";:COLOR 7,0
14390 LOCATE 16,39:PRINT"10 GARANT       ";:COLOR 0,7:PRINT"    ";:COLOR 7,0
14400 LOCATE 18,39:PRINT"11 EINW. ARZT   ";:COLOR 0,7:PRINT"                         ";:COLOR 7,0
14410 LOCATE 20,39:PRINT"12 STATION      ";:COLOR 0,7:PRINT"    ";:COLOR 7,0
14998 COLOR 7,0
14999 RETURN
15000 REM *********************************************************************
15999 RETURN
16000 REM *********************************************************************
16010 REM *** GET INPUT BUFFER FROM COM
16020 REM *********************************************************************
16025 INBUFF$=""
16030 IF LOC(1) = 0 GOTO 16030
16040 LINE INPUT #1,INBUFF$
16070 IF LOC(1) > 0 THEN GOTO 16025
16999 RETURN
17000 REM *********************************************************************
17010 REM *** OPEN SERIAL COM1 2400/8/N/1
17020 REM *********************************************************************
17030 OPEN "com1:2400,N,8,,CS,DS,CD" AS #1
17999 RETURN
18000 REM *********************************************************************
18010 REM *** CLOSE SERIAL COM1 2400/8/N/1
18020 REM *********************************************************************
18030 CLOSE 1
18999 RETURN
19000 REM *********************************************************************
19010 REM *** DISPLAY MESSAGE
19020 REM *********************************************************************
19030 COLOR 0,7:LOCATE 22,16:PRINT MESSAGE$
19499 RETURN
19500 REM CLEAR MESSAGES BOX
19510 COLOR 0,7:LOCATE 23,16
19520 LOCATE 22,1:PRINT "DIALOG BOX:                                                                     ";
19999 RETURN
21000 REM *********************************************************************
21010 REM *** INIT MCS UNIT and SYSTEM VARIABLE and OPEN COM1 1200/8/N/1
21020 REM *********************************************************************
21040 CR$=CHR$(13)
21050 ESC$=CHR$(27)
21055 FS$=CHR$(28)
21060 BARCODE$="B"
21070 REMON$="E"
21080 REMOFF$="F"
21090 READ1$="G1"
21100 READ2$="G2"
21110 READ3$="G3"
21120 READ4$="G4"
21130 RETCARD$="H"
21140 WRITE1$="I1"
21150 WRITE2$="I2"
21160 WRITE3$="I3"
21170 PRINTERON$="J"
21180 PRINTEROFF$="K"
21190 PROTOKOLLON$="L"
21200 PROTOKOLLOFF$="M"
21210 PRINTOUT$="P"
21220 READON$="R"
21230 TRACK1$="T1"
21240 TRACK2$="T2"
21250 TRACK3$="T3"
21260 WRITEON$="W"
21270 STATUS$="?"
21490 REM ---------------------------------------------------------------------
21500 REM INIT UNIT TO REMOTE MODE
21510 PRINT #1,ESC$;REMON$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
21520 FOR I = 1 TO 500: NEXT
21999 RETURN
22000 REM *********************************************************************
22010 REM *** RETURN CARD
22020 REM *********************************************************************
22030 PRINT #1,ESC$;RETCARD$;CR$;:LINE INPUT #1,INBUFF$:LOCATE 5,79:PRINT INBUFF$;
22999 RETURN
25000 REM *********************************************************************
25010 REM *** ERROR HANDLER
25020 REM *********************************************************************
25030 RUN
26000 REM *********************************************************************
26010 REM *** DELAY FOR PIEPS
26020 REM *********************************************************************
26030 FOR I = 0 TO 500
26040 NEXT
26999 RETURN
30000 REM ********************************************************************
30010 REM *** AENDERUNGSROUTINE
30020 REM ********************************************************************
30025 IN$=INPUT$(1):IF IN$=CHR$(8) THEN GOTO 6500
30030 ON ERROR GOTO 25000
30035 FOR I=1 TO 500:NEXT I:LOCATE 21,20:PRINT STRING$(50," ")
30037 FELD$="A"
30040 LOCATE 21,1:PRINT "DIALOG BOX:    ----> AENDERUNGS-NR.:";:PRINT "  ";:LINE INPUT NUM$
30045 IF NUM$=CHR$(13) THEN FELD$="":GOTO 6500
30050 NUMB=VAL(NUM$)
30060 ON NUMB GOSUB 6210,6220,6260,6270,6277,6300,6310,6312,6322,6350,6356,6358
30070 IF NUMB >12 OR NUMB <1 THEN GOTO 30000
30080 RETURN 40000
30090 REM ********************************************************************
31000 REM *** FELDKONTROLLE
31010 REM *********************************************************************
31020 ON ERROR GOTO 25000
31040 EIN$(I)=INPUT$(1):IF EIN$(I)=CHR$(8) GOTO 6500
31050 RETURN
33000 REM *********************************************************************
33010 REM *** PRINT ETIKETTEN NACH FORMAT MCP
33020 REM *********************************************************************
33030 COLOR 0,7:LOCATE 21,1:PRINT "DIALOG BOX:    ----> AUSDRUCK DER DATEN LŽUFT!!                                 ";
33040 PRINT #1,ESC$;PRINTERON$;CR$;:LINE INPUT #1,INBUFF$
33050 PRINT #1,FS$;WRITE1$;BUFFER1$;CR$;
33060 PRINT #1,FS$;WRITE2$;BUFFER2$;CR$;
33070 PRINT #1,FS$;WRITE3$;BUFFER3$;CR$;
33190 PRINT #1,ESC$;PRINTEROFF$;CR$;:LINE INPUT #1,INBUFF$
33200 LOCATE 5,79:PRINT INBUFF$;
33210 GOSUB 13000
33220 COLOR 7,0:LOCATE 21,1:PRINT "DIALOG BOX:    ----> AUSDRUCK DER DATEN FERTIG !!                               ";
33230 RETURN
34000 REM *********************************************************************
34010 REM *** ETIKETTEN-AUSDRUCK šBER PAR.-DRUCKER
34020 REM *********************************************************************
34030 COLOR 0,7:LOCATE 21,1:PRINT "DIALOG BOX:    ----> AUSDRUCK DER DATEN LŽUFT!!                                 ";
34040 REM LPRINT CHR$(10);
34050 LPRINT "      ";PATNUM$;" ";ABT$;
34060 GOSUB 26000:GOSUB 26000
34070 LPRINT CHR$(10);
34080 LPRINT "      ";PATNAME$;
34090 GOSUB 26000:GOSUB 26000
34100 LPRINT CHR$(10);
34102 LPRINT "      ";VORNAME$;
34104 GOSUB 26000:GOSUB 26000
34106 LPRINT CHR$(13);
34110 LPRINT "      ";GEBDAT$;
34120 GOSUB 26000:GOSUB 26000
34130 LPRINT CHR$(13);
34140 REM LPRINT CHR$(13);
34150 FOR I=1 TO 2:LPRINT CHR$(10):NEXT I
34160 LOCATE 4,79:PRINT INBUFF$;
34170 GOSUB 13000
34180 COLOR 7,0:LOCATE 21,1:PRINT "DIALOG BOX:    ----> AUSDRUCK DER DATEN FERTIG !!                               ";
34190 RETURN
35000 REM *********************************************************************
40000 REM *********************************************************************
40010 REM *** AENDERUNGEN J/N
40020 REM *********************************************************************
40030 LOCATE 21,20:PRINT STRING$(50," ")
40040 LOCATE 21,1:PRINT "DIALOG BOX:    ----> AENDERUNGEN? (J/N) ";:PRINT " ";:LINE INPUT A$
40055 IF A$="N" OR A$="n" THEN GOTO 40070
40065 IF A$="J" OR A$="j" THEN GOSUB 30030 ELSE GOTO 40040
40070 RETURN 6500
41000 REM *********************************************************************
41010 REM *** PRUEFZIFFER-RECHNUNG
41020 REM *********************************************************************
41030 QUER=0
41040 PATNUM=VAL(PATNUM$)
41050 FOR I=1 TO 9
41060 MUL$(I) = MID$(PATNUM$,I,1)
41070 MUL(I) = VAL(MUL$(I))
41080 IF I=1 THEN FAK=2 ELSE IF I=2 THEN FAK=7 ELSE IF I=3 THEN FAK=6 ELSE IF I=4 THEN FAK=5 ELSE IF I=5 THEN FAK=4 ELSE IF I=6 THEN FAK=0 ELSE IF I=7 THEN FAK=3 ELSE IF I=8 THEN FAK=2 ELSE IF I=9 THEN FAK=0
41090 EMUL(I)=MUL(I)*FAK:QUER=QUER+EMUL(I)
41100 NEXT I
41110 QSUM=QUER/10:QSUM$=STR$(QSUM):REST$=RIGHT$(QSUM$,1):REST=VAL(REST$)
41120 ZEHN=10:PZ=ZEHN-REST:PZ$=STR$(PZ)
41130 PATNUM$=LEFT$(PATNUM$,9):PATNUM$=PATNUM$+RIGHT$(PZ$,1)
41140 RETURN
41150 REM *********************************************************************
